import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
    id "com.github.johnrengelman.shadow" version "7.1.0"
}

apply plugin: 'idea'
apply plugin: 'java-library'
apply plugin: 'maven-publish'

group = 'fr.hyriode'
version = '1.0.4'
archivesBaseName = 'HyriAPI'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(16)
    }
}

compileJava {
    sourceCompatibility = JavaVersion.VERSION_16
    options.encoding = 'UTF-8'
}

repositories {
    mavenCentral()
}

shadowJar {
    configurations = [project.configurations.shadow]
    exclude 'fr/hyriode/hyriapi/impl'
    exclude 'plugin.yml'
    classifier ''
}

task implJar(type: ShadowJar) {
    archiveBaseName.set('HyriAPI-Impl')
    from sourceSets.main.output
    configurations = [project.configurations.shadow]
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    classifier 'javadoc'
    from javadoc
}

tasks.withType(Javadoc) {
    options.encoding = 'UTF-8'
}

artifacts {
    archives sourcesJar, javadocJar
}

publishing {
    publications {
        maven(MavenPublication) { publication ->
            project.shadow.component(publication)
            artifact tasks.sourcesJar
            artifact tasks.javadocJar

            pom {
                groupId = project.group
                artifactId = 'hyriapi'
                version = project.version
                name = 'HyriAPI'
                description = 'Project with the abstract layer of HyriAPI'
                url = 'https://github.com/Hyriode/HyriAPI'
            }
        }
    }

    repositories {
        maven {
            credentials {
                username System.getenv('USERNAME')
                password System.getenv('TOKEN')
            }
            name = "HyriAPI_maven"
            url = "https://maven.pkg.github.com/Hyriode/HyriAPI"
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    shadow 'redis.clients:jedis:3.7.0'
    shadow 'com.google.code.gson:gson:2.8.9'
    shadow 'org.slf4j:slf4j-nop:1.7.32'
}

processResources {
    filter {
        it.replace('@version@', project.version)
    }
}