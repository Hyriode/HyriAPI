import java.nio.charset.StandardCharsets
import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
    id "com.github.johnrengelman.shadow" version "6.0.0"
}

apply plugin: 'idea'
apply plugin: 'java-library'
apply plugin: 'maven-publish'

group = 'fr.hyriode'
version = '1.0.0 SNAPSHOT'
archivesBaseName = 'HyriAPI'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
}

compileJava {
    targetCompatibility = sourceCompatibility = JavaVersion.VERSION_1_8
    options.encoding = StandardCharsets.UTF_8
}

repositories {
    mavenCentral()

    maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }

    maven { url = 'https://oss.sonatype.org/content/groups/public/' }

    maven {
        name = 'HyggdrasilConnector_maven'
        url 'https://maven.pkg.github.com/Hyriode/HyggdrasilConnector/'
        credentials {
            username System.getenv('USERNAME')
            password System.getenv('TOKEN')
        }
    }
}

shadowJar {
    configurations = [project.configurations.shadow]
    exclude 'fr/hyriode/hyriapi/impl'
}

task implJar(type: ShadowJar) {
    archiveBaseName.set('Impl')
    from(project.convention.getPlugin(JavaPluginConvention).sourceSets.main.output)
    configurations = [project.configurations.shadow]
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    classifier 'javadoc'
    from javadoc
}

artifacts {
    archives sourcesJar, javadocJar
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifact tasks.sourcesJar
            artifact tasks.javadocJar

            pom {
                groupId = project.group
                artifactId = 'hyriapi'
                version = project.version
                name = 'HyriAPI'
                description = 'Project with abstract layer of HyriAPI'
                url = 'https://github.com/Hyriode/HyriAPI'
            }
        }
    }

    repositories {
        maven {
            credentials {
                username System.getenv('USERNAME')
                password System.getenv('TOKEN')
            }
            name = "HyriAPI_maven"
            url = "https://maven.pkg.github.com/Hyriode/HyriAPI"
        }
    }
}

dependencies {
    shadow fileTree(include: ['*.jar'], dir: 'libs')
    shadow 'org.spigotmc:spigot-api:1.8.8-R0.1-SNAPSHOT'
    shadow 'fr.hyriode:hyggdrasil-connector:1.0.12-SNAPSHOT'
    shadow 'redis.clients:jedis:3.6.1'
}